buildscript {
    repositories {
        mavenCentral()

        maven {
            name 'forge'
            url 'https://maven.minecraftforge.net'
        }
        maven {
            // GTNH RetroFuturaGradle and ASM Fork
            name "GTNH Maven"
            url "http://jenkins.usrv.eu:8081/nexus/content/groups/public/"
        }
        maven {
            name 'sonatype'
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
            name 'Scala CI dependencies'
            url 'https://repo1.maven.org/maven2/'
        }

        mavenLocal()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

apply plugin: "idea"
apply plugin: 'net.minecraftforge.gradle.forge'

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

if (enableModernJavaSyntax.toBoolean()) {
    repositories {
        mavenCentral()
    }

    dependencies {
        annotationProcessor 'com.github.bsideup.jabel:jabel-javac-plugin:1.0.0'
        // workaround for https://github.com/bsideup/jabel/issues/174
        annotationProcessor 'net.java.dev.jna:jna-platform:5.13.0'
        compileOnly('com.github.bsideup.jabel:jabel-javac-plugin:1.0.0') {
            transitive = false // We only care about the 1 annotation class
        }
    }

    configure([tasks.compileJava]) {
        sourceCompatibility = 17 // for the IDE support

        options.compilerArgs = [
            "--release", "8",
            '--enable-preview',
        ]

        doFirst {
            // Can be omitted on Java 14 and higher
            options.compilerArgs << '-Xplugin:jabel'

            options.compilerArgs = options.compilerArgs.findAll {
                it != '--enable-preview'
            }
        }
    }
}

group = "com.github.GTNewHorizons"
archivesBaseName = modId

minecraft {
    version = project.minecraftVersion + "-" + project.forgeVersion + "-" + project.minecraftVersion
    mappings = project.channel + '_' + project.mappingsVersion
}

repositories {
    maven {
        name 'Overmind forge repo mirror'
        url 'https://gregtech.overminddl1.com/'
    }
    maven {
        name = "GTNH Maven"
        url = "http://jenkins.usrv.eu:8081/nexus/content/groups/public/"
    }
    maven {
        name 'sonatype'
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else. replaces ${key} with value in text
    filesMatching("mcmod.info")  {
        expand "minecraftVersion": project.minecraft.version,
            "modVersion": modVersion,
            "modId": modId,
            "modName": modName
    }
}
