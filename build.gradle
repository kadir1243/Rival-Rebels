plugins {
    id 'idea'
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id 'maven-publish'
    id "com.modrinth.minotaur" version "2.+"
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

loom {
    accessWidenerPath = file("src/main/resources/${mod_id}.accesswidener")

    runs {
        datagen {
            server()
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"

            ideConfigGenerated = true
            runDir "build/datagen"
        }
        datagenClient {
            client()
            name "Data Generation Client"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"

            ideConfigGenerated = true
            runDir "build/datagen"
        }
    }
}

repositories {
    maven {
        name = "Fuzs Mod Resources"
        url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
    }
    maven { url 'https://maven.parchmentmc.org' }
}

dependencies {
    // to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    mappings loom.layered() {
        officialMojangMappings()

        parchment("org.parchmentmc.data:parchment-1.21:${project.mapping_version}@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modApi "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:21.0.8"
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
        minecraft_version: minecraft_version,
        mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
        mod_authors: mod_authors, mod_description: mod_description,
    ]
    inputs.properties replaceProperties

    filesMatching('fabric.mod.json') {
        expand replaceProperties + [project: project]
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release = 21
}


sourceSets {
    main {
        resources {
            srcDirs += [
                'src/main/generated'
            ]
        }
    }
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_21
}

if (System.getenv("MODRINTH_TOKEN") != null) {
    project.setProperty("MODRINTH_TOKEN", System.getenv("MODRINTH_TOKEN"))
}

modrinth {
    token = project.findProperty("MODRINTH_TOKEN")
    projectId = "rival-rebels"
    versionType = "alpha"
    uploadFile = remapJar
    gameVersions = ["1.21.1"]
    loaders = ["fabric"]
    dependencies {
        required.project "fabric-api"
        required.project "forge-config-api-port"
    }
}
